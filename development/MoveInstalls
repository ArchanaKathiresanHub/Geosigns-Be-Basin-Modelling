#!/bin/csh
set version = "v2013.01nightly"
if ($# == 1) then
   set version = $1
endif
set installDirectory = "/apps/sssdev/ibs"
set installsToMove = "${version}_b ${version}_a ${version}"
set installToRemove = "${version}_c"
set installToMoveTo = $installToRemove

if (-d $installDirectory/$installToRemove) then
      echo "$installDirectory/$installToRemove exists, removing it"
   rm -rf $installDirectory/$installToRemove
   echo
endif
foreach installToMove ($installsToMove)
   if (-d $installDirectory/$installToMove) then
      echo "$installDirectory/$installToMove exists"
      if (! -d  $installDirectory/$installToMoveTo) then
	 echo "$installDirectory/$installToMoveTo does not exist"
	 echo "moving $installDirectory/$installToMove to $installDirectory/$installToMoveTo"
	 mv $installDirectory/$installToMove $installDirectory/$installToMoveTo
      else
	 echo "$installDirectory/$installToMoveTo exists"
	 echo "Cannot move $installDirectory/$installToMove to $installDirectory/$installToMoveTo"
	 exit 1
      endif
   else
      echo "$installDirectory/$installToMove does not exist"
      echo "Nothing to move"
   endif
   set installToMoveTo = $installToMove
   echo
end
exit 0
