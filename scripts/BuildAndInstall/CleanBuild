#!/bin/csh

set echo # debug

## Setting variables
setenv IBS_CLEANBUILDANDINSTALL true
# Repository targets
set developmentbranch = "Trunk/development"
set geocasedevelopmentbranch = "trunk/development/geocase"
set thirdpartybranch = "Trunk/3rdparty"
set version = "v2013-CI"
set svnrepo = svn://amsd2a-n-b01001.europe.shell.com
# Executable paths
set path = (/usr/bin/X11 /usr/bin/ $path)
set path = ($HOME/bin $HOME/bin.`uname` $path)
set path = (/glb/home/ksaho3/Eclipse/TF/TEE-CLC-11.0.0 $path)

# TFS specifics
set TF = tf
set projectroot = '$/Basin Modeling'
set rootpath = "/scratch/`whoami`/CleanBuildAndInstall"

set WORKSPACE = CleanBuild
set COLLECTION = https://tfs.sede-coe.pds.nl/tfs/COE-II

while ($#argv > 0)
   switch ($1)
      case -branch:
      shift
      set developmentbranch = "$1"
      breaksw
      case -development:
      shift
      set developmentbranch = "$1"
      breaksw
      case -svnbranch:
      case -subversionbranch:
      shift
      set geocasedevelopmentbranch = "$1/development/geocase"
      breaksw
      case -thirdparty:
      case -3rdparty:
      shift
      set thirdpartybranch = "$1"
      breaksw
      case -version:
      shift
      set version = "$1"
      breaksw
   endsw
   shift
end

set developmentdirectory = "$developmentbranch"
set geocasedevelopmentdirectory = "$developmentdirectory/geocase"
set geocasemiscdevelopmentdirectory = "$geocasedevelopmentdirectory/misc"
set thirdpartydirectory = "3rdparty"

echo "Deleting workspace"
tf workspace -noprompt -delete -collection:$COLLECTION $WORKSPACE
echo "Creating workspace"
tf workspace -noprompt -new -collection:$COLLECTION $WORKSPACE -comment:"Clean Build"
echo "Mapping workspace to $rootpath"
tf workfold  -noprompt -map -collection:$COLLECTION -workspace:$WORKSPACE \$/Basin\ Modeling/IBS $rootpath

## Clean and create directory structure
echo "Creating base directory $rootpath"
rm -rf $rootpath
mkdir -p $rootpath

## Retrieve from TFS
# Get into the mapped directory of the desired workspace
cd $rootpath

echo Get the latest versions
echo "$TF get -recursive -force $developmentdirectory"
eval $TF get -recursive -force $developmentdirectory

echo "$TF get -recursive -force $thirdpartybranch/geocosm.tar"
eval $TF get -recursive -force $thirdpartybranch/geocosm.tar

cp `find . -name geocosm.tar` $rootpath/$developmentdirectory

echo "Check out svn-based legacy code"
cd $rootpath/$geocasedevelopmentdirectory
svn co "$svnrepo/$geocasedevelopmentbranch/BB" BB

cd $rootpath/$geocasemiscdevelopmentdirectory
svn co "$svnrepo/$geocasedevelopmentbranch/misc/GospelScriptFiles" GospelScriptFiles

echo "Change to the development directory $rootpath/$developmentdirectory"
cd $rootpath/$developmentdirectory

## Build all
switch ( `whoami`)
    case ksaho3:
	setenv BUILDINSTALLADDRESS alfred.vanderhoeven@shell.com
	breaksw
    case nlnku3:
	setenv BUILDINSTALLADDRESS natalya.kuznetsova@shell.com
	breaksw
    case nlnwse0:
	setenv BUILDINSTALLADDRESS william.senior@shell.com
	breaksw
    case nlamuv:
	setenv BUILDINSTALLADDRESS alfredo.muela@shell.com
	breaksw
    case nlskui:
	setenv BUILDINSTALLADDRESS sreejith.kuttanikkad@shell.com
	breaksw
    case nlvam0:
	setenv BUILDINSTALLADDRESS vijaya.ambati@shell.com
	breaksw
    case nlrnig:
	setenv BUILDINSTALLADDRESS ruben.nijp@shell.com
	breaksw
    case nlskp6:
	setenv BUILDINSTALLADDRESS stefan.kollet@shell.com
	breaksw
    case nlome0:
	setenv BUILDINSTALLADDRESS olivier.meuric@shell.com
	breaksw
    case *:
	setenv BUILDINSTALLADDRESS alfred.vanderhoeven@shell.com
	breaksw
 endsw

echo "Sending emails to: $BUILDINSTALLADDRESS"

echo "Performing BuildAll of $version"
./BuildAll >& BuildAll.log
set exitStatus = $status
if ($exitStatus != 0) then
    echo "Failed to complete BuildAll of $version"
    set MAILSUBJECT="failed to complete BuildAll of $version"
    cat BuildAll.log | mail -s "$MAILSUBJECT" $BUILDINSTALLADDRESS
    echo "+++++++++ e-mail content sent +++++++++"
    echo "Sent to: " $BUILDINSTALLADDRESS 
    echo "Subject: " $MAILSUBJECT
    cat BuildAll.log
    echo "+++++++++++++++++++++++++++++++++++++++"
   exit 1
endif

chgrp -R g_psaz00 .
chmod -R g+rw .

set MAILSUBJECT="Completed BuildAll of $version"
cat BuildAll.log | mail -s "$MAILSUBJECT" $BUILDINSTALLADDRESS
echo "+++++++++ e-mail content sent +++++++++"
echo "Sent to:" $BUILDINSTALLADDRESS
echo "Subject:" $MAILSUBJECT
cat BuildAll.log
echo "+++++++++++++++++++++++++++++++++++++++"
exit 0

