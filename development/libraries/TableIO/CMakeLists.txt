#########################################################################
#                                                                       #
# Copyright (C) 2012-2013 Shell International Exploration & Production. #
# All rights reserved.                                                  #
#                                                                       #
# Developed under license for Shell by PDS BV.                          #
#                                                                       #
# Confidential and proprietary source code of Shell.                    #
# Do not distribute without written permission from Shell.              #
#                                                                       #
#########################################################################

# Building the TableIO library
#
# ATTENTION: The file is used by Basin Modeling and Geocase.
#
include_directories(src)

set (generator_source_files
  ${CMAKE_CURRENT_BINARY_DIR}/cauldronschema.C 
  ${CMAKE_CURRENT_BINARY_DIR}/cauldronschemafuncs.C
  ${CMAKE_CURRENT_BINARY_DIR}/cauldronschema.h
  ${CMAKE_CURRENT_BINARY_DIR}/cauldronschemafuncs.h
  )

add_library( TableIO
	src/dataschema.C
	src/database.C
	src/datautils.C
	${generator_source_files}
)
include_directories(${CMAKE_CURRENT_BINARY_DIR})

# cauldronschema and cauldronschemafuncs have to be generated by the the 'parspec' tool
if (CMAKE_CROSSCOMPILING)
  set(IMPORT_EXECUTABLES "IMPORTFILE-NOTFOUND" CACHE FILEPATH "The location of the export file from a native build")
  include(${IMPORT_EXECUTABLES})
endif()

if (NOT CMAKE_CROSSCOMPILING)
   add_executable( parspec 
      src/parspec.C
      src/datautils.C
   )
endif()

add_custom_command( OUTPUT ${generator_source_files}
	DEPENDS parspec BasinModellerProperty.Spec
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
	COMMAND parspec -schema CauldronSchema -directory ${CMAKE_CURRENT_BINARY_DIR} -input BasinModellerProperty.Spec 
)

if (NOT CMAKE_CROSSCOMPILING)
   EXPORT(TARGETS parspec FILE ${CMAKE_BINARY_DIR}/ImportExecutables.cmake )
endif()

generate_dox( src/tableio.cfg )

install(TARGETS TableIO
	RUNTIME DESTINATION bin
	LIBRARY	DESTINATION lib
	ARCHIVE DESTINATION lib
)

