#########################################################################
#                                                                       #
# Copyright (C) 2012-2013 Shell International Exploration & Production. #
# All rights reserved.                                                  #
#                                                                       #
# Developed under license for Shell by PDS BV.                          #
#                                                                       #
# Confidential and proprietary source code of Shell.                    #
# Do not distribute without written permission from Shell.              #
#                                                                       #
#########################################################################

# Building the TableIO library
#
# ATTENTION: The file is used by Basin Modeling and Geocase.
#
include_directories(src)

set (generator_source_files
  ${CMAKE_CURRENT_BINARY_DIR}/cauldronschema.cpp
  ${CMAKE_CURRENT_BINARY_DIR}/cauldronschemafuncs.cpp
  ${CMAKE_CURRENT_BINARY_DIR}/cauldronschema.h
  ${CMAKE_CURRENT_BINARY_DIR}/cauldronschemafuncs.h
)

add_library( TableIO
	src/dataschema.cpp
	src/database.cpp
	src/datautils.cpp
	src/ProjectFileHandler.h
	src/ProjectFileHandler.cpp
	src/upgradeTables.cpp
	${generator_source_files}
)

# under Linux we are building Boost that is why this target exist
if (UNIX)
add_dependencies(TableIO Boost)
endif (UNIX)

include_directories(${CMAKE_CURRENT_BINARY_DIR})

add_dependencies( TableIO
   FileSystem
   utilities
)

bm_include_libraries( TableIO
   FileSystem
   utilities
)

target_link_libraries( TableIO
   FileSystem
   utilities
)


# cauldronschema and cauldronschemafuncs have to be generated by the the 'parspec' tool
if (CMAKE_CROSSCOMPILING)
  set(IMPORT_EXECUTABLES "IMPORTFILE-NOTFOUND" CACHE FILEPATH "The location of the export file from a native build")
  include(${IMPORT_EXECUTABLES})
endif()

if (NOT CMAKE_CROSSCOMPILING)
   add_executable( parspec
      src/parspec.cpp
      src/datautils.cpp
   )
endif()

add_custom_command( OUTPUT ${generator_source_files}
	DEPENDS parspec BasinModellerProperty.Spec
	WORKING_DIRECTORY ${CMAKE_CURRENT_SOURCE_DIR}
	COMMAND parspec -schema CauldronSchema -directory ${CMAKE_CURRENT_BINARY_DIR} -input BasinModellerProperty.Spec
)

if (NOT CMAKE_CROSSCOMPILING)
   EXPORT(TARGETS parspec FILE ${CMAKE_BINARY_DIR}/ImportExecutables.cmake )
endif()

generate_dox( src/tableio.cfg )

install(TARGETS TableIO
	RUNTIME DESTINATION bin
	LIBRARY	DESTINATION lib
	ARCHIVE DESTINATION lib
)

configure_file(
      ${CMAKE_CURRENT_SOURCE_DIR}/test/Reference.project3d
      ${CMAKE_CURRENT_BINARY_DIR}
      COPYONLY
)
configure_file(
      ${CMAKE_CURRENT_SOURCE_DIR}/test/ReferenceWithOutput.project3d
      ${CMAKE_CURRENT_BINARY_DIR}
      COPYONLY
)

file(MAKE_DIRECTORY ${CMAKE_CURRENT_BINARY_DIR}/ReferenceWithOutput_CauldronOutputDir)

configure_file(
      ${CMAKE_CURRENT_SOURCE_DIR}/test/ReferenceWithOutput_CauldronOutputDir/Output.iotables3d
      ${CMAKE_CURRENT_BINARY_DIR}/ReferenceWithOutput_CauldronOutputDir
      COPYONLY
)

add_gtest( NAME TableIO::DataSchemaTest
           SOURCES test/DataSchemaTest.cpp
           LIBRARIES TableIO utilities
)

add_gtest( NAME TableIO::DataBaseTest
           SOURCES test/DataBaseTest.cpp
           LIBRARIES TableIO utilities
)

add_gtest( NAME TableIO::ProjectFileHandlerTest
           SOURCES test/ProjectFileHandlerTest.cpp
           LIBRARIES TableIO utilities
)
